import "pkg:/source/enums/ColorPalette.bs"
import "pkg:/source/enums/PosterLoadStatus.bs"
import "pkg:/source/utils/misc.bs"

sub init()
    m.channelLogo = m.top.findNode("channelLogo")

    m.borderTop = m.top.findNode("borderTop")
    m.borderTop.color = ColorPalette.MIDGREY

    m.backdrop = m.top.findNode("backdrop")
    m.backdrop.color = ColorPalette.BLACK90

    m.channelTitle = m.top.findNode("channelTitle")
    m.channelTitle.font.size = 27
    m.channelTitle.color = ColorPalette.OFFWHITE
end sub

sub onLoadStatusChanged()
    if m.channelLogo.loadStatus <> PosterLoadStatus.LOADING
        m.channelLogo.unobserveFieldScoped("loadStatus")
    end if

    if m.channelLogo.loadStatus = PosterLoadStatus.READY
        m.channelLogo.visible = true
        return
    end if
end sub

sub onWidthChanged()
    logoWidth = int(m.top.width / 3)

    titleTranslationHorizontal = logoWidth + 10
    titleWidth = m.top.width - titleTranslationHorizontal

    m.channelLogo.width = logoWidth
    m.channelTitle.maxWidth = titleWidth
    m.channelTitle.translation = `[${titleTranslationHorizontal}, 0]`

    m.borderTop.width = m.top.width
    m.backdrop.width = m.top.width
end sub

sub onHeightChanged()
    m.channelLogo.height = m.top.height
    m.backdrop.height = m.top.height
    m.channelTitle.height = m.top.height
end sub

sub contentChanged()
    itemData = m.top.content

    if not isValid(itemData) then return

    ' Reset all values due to Roku's component reuse
    m.channelLogo.uri = ""
    m.channelTitle.text = ""

    if isValidAndNotEmpty(itemData.LookupCI("hdsmalliconurl"))
        m.channelLogo.observeFieldScoped("loadStatus", "onLoadStatusChanged")
        m.channelLogo.uri = itemData.LookupCI("hdsmalliconurl")
    end if

    if isValid(itemData.LookupCI("title"))
        m.channelTitle.text = itemData.LookupCI("title")
    end if

    if isValidAndNotEmpty(m.channelLogo.uri) and isValidAndNotEmpty(m.channelTitle.text)
        m.channelLogo.visible = true
        m.channelTitle.visible = true
    else if isValidAndNotEmpty(m.channelTitle.uri)
        m.channelLogo.visible = true
        m.channelTitle.visible = false
    else
        m.channelLogo.visible = false
        m.channelTitle.visible = true
    end if

end sub
