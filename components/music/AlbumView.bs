import "pkg:/source/enums/ColorPalette.bs"
import "pkg:/source/enums/KeyCode.bs"
import "pkg:/source/enums/String.bs"
import "pkg:/source/enums/TaskControl.bs"
import "pkg:/source/utils/misc.bs"

sub init()
    m.top.optionsAvailable = false
    setupMainNode()

    m.instantMix = m.top.findNode("instantMix")
    m.instantMix.text = tr("Instant Mix")
    m.instantMix.background = ColorPalette.LIGHTBLUE
    m.instantMix.color = ColorPalette.DARKGREY
    m.instantMix.focusBackground = chainLookupReturn(m.global.session, "user.settings.colorCursor", ColorPalette.HIGHLIGHT)
    m.instantMix.focusColor = ColorPalette.WHITE

    m.albumCover = m.top.findNode("albumCover")
    m.songList = m.top.findNode("songList")
    m.infoGroup = m.top.FindNode("infoGroup")
    m.songListRect = m.top.FindNode("songListRect")
    m.top.lastFocus = m.songList

    m.songList.focusFootprintBitmapUri = string.EMPTY
    m.songList.focusBitmapBlendColor = chainLookupReturn(m.global.session, "user.settings.colorCursor", ColorPalette.HIGHLIGHT)
    m.songList.focusFootprintBlendColor = ColorPalette.LIGHTHIGHLIGHT

    m.songListRect.color = ColorPalette.ELEMENTBACKGROUND

    m.songList.observeField("doneLoading", "onDoneLoading")

    m.dscr = m.top.findNode("overview")
    m.dscr.ellipsisText = tr("...")
end sub

sub setupMainNode()
    main = m.top.findNode("toplevel")
    main.translation = [96, 175]
end sub

' Set values for displayed values on screen
sub pageContentChanged()
    item = m.top.pageContent

    setPosterImage(item.posterURL)
    setScreenTitle(item.json)
    setOnScreenTextValues(item.json)
end sub

' Set poster image on screen
sub setPosterImage(posterURL)
    if isValid(posterURL)
        m.albumCover.uri = posterURL
    end if
end sub

' Set screen's title text
sub setScreenTitle(json)
    newTitle = ""
    if isValid(json)
        if isValid(json.AlbumArtist)
            newTitle = json.AlbumArtist
        end if
        if isValid(json.AlbumArtist) and isValid(json.name)
            newTitle = newTitle + " / "
        end if
        if isValid(json.name)
            newTitle = newTitle + json.name
        end if
    end if
    m.top.overhangTitle = newTitle
end sub

' Adjust scene by removing overview node and showing more songs
sub adjustScreenForNoOverview()
    m.infoGroup.removeChild(m.dscr)
    m.songListRect.height = 800
    m.songList.numRows = 12
end sub

sub OnScreenShown()
    overhang = m.top.getScene().findNode("overhang")

    if not overhang.isVisible
        overhang.isVisible = true
    end if

    if isValid(m.top.lastFocus)
        m.top.lastFocus.setFocus(true)
        group = m.global.sceneManager.callFunc("getActiveScene")
        group.lastFocus = m.top.lastFocus
    else
        m.top.setFocus(true)
        group = m.global.sceneManager.callFunc("getActiveScene")
        group.lastFocus = m.top
    end if
end sub

' Populate on screen text variables
sub setOnScreenTextValues(json)
    if isValid(json)
        if isValid(json.overview) and json.overview <> ""
            ' We have overview text
            setFieldTextValue("overview", json.overview)
        else
            ' We don't have overview text
            adjustScreenForNoOverview()
        end if

        setFieldTextValue("numberofsongs", tr("%1 Tracks").Replace("%1", stri(json.ChildCount)))

        if type(json.ProductionYear) = "roInt"
            setFieldTextValue("released", tr("Released %1").Replace("%1", stri(json.ProductionYear)))
        end if

        if json.genres.count() > 0
            setFieldTextValue("genres", json.genres.join(", "))
        end if

        if type(json.RunTimeTicks) = "LongInteger"
            setFieldTextValue("runtime", tr("%1 mins").Replace("%1", stri(getMinutes(json.RunTimeTicks))))
        end if
    end if
end sub

function onKeyEvent(key as string, press as boolean) as boolean
    if not press then return false

    if key = KeyCode.OPTIONS
        if m.songList.hasFocus()
            focusedSong = m.songList.content.getChild(m.songList.itemFocused)
            if not isValidAndNotEmpty(focusedSong) then return false

            dialogData = [tr("Add To Playlist")]
            m.global.sceneManager.callFunc("optionDialog", "libraryitem", focusedSong.LookupCI("title") ?? tr("Options"), [], dialogData, { id: focusedSong.LookupCI("id") })

            return true
        end if
    end if

    if key = KeyCode.RIGHT
        if m.instantMix.hasFocus()
            m.top.lastFocus = m.songList
            m.songList.setFocus(true)
            m.instantMix.focus = false
            group = m.global.sceneManager.callFunc("getActiveScene")
            group.lastFocus = m.songList
            return true
        end if
    end if

    if key = KeyCode.UP
        if m.songList.hasFocus()
            if m.dscr.isTextEllipsized
                m.dscr.setFocus(true)
                m.dscr.color = chainLookupReturn(m.global.session, "user.settings.colorCursor", ColorPalette.HIGHLIGHT)
                return true
            end if
        end if
    end if

    if key = KeyCode.OK
        if m.dscr.hasFocus()
            createFullDscrDlg()
            return true
        end if
        if m.instantMix.hasFocus()
            m.instantMix.selected = not m.instantMix.selected
            return true
        end if
    end if

    if key = KeyCode.DOWN
        if m.dscr.hasFocus()
            m.songList.setFocus(true)
            m.dscr.color = ColorPalette.WHITE
            return true
        end if
    end if

    if key = KeyCode.LEFT
        if m.songList.hasFocus()
            if m.instantMix.visible
                m.top.lastFocus = m.instantMix
                m.instantMix.setFocus(true)
                m.instantMix.focus = true
                group = m.global.sceneManager.callFunc("getActiveScene")
                group.lastFocus = m.instantMix
                return true
            end if
        end if
    end if

    return false
end function

sub createFullDscrDlg()
    if isAllValid([m.top.overhangTitle, m.dscr.text])
        m.global.sceneManager.callFunc("standardDialog", m.top.overhangTitle, { data: ["<p>" + m.dscr.text + "</p>"] })
    end if
end sub

sub onDoneLoading()
    m.songList.unobservefield("doneLoading")
    stopLoadingSpinner()
end sub
