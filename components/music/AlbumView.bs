import "pkg:/source/enums/ColorPalette.bs"
import "pkg:/source/enums/KeyCode.bs"
import "pkg:/source/enums/MediaPlaybackState.bs"
import "pkg:/source/enums/String.bs"
import "pkg:/source/enums/TaskControl.bs"
import "pkg:/source/enums/ViewLoadStatus.bs"
import "pkg:/source/utils/misc.bs"

sub init()
    m.loadStatus = ViewLoadStatus.INIT

    ' Load background image
    m.LoadBackdropImageTask = CreateObject("roSGNode", "LoadItemsTask")
    m.LoadBackdropImageTask.itemsToLoad = "backdropImage"
    m.backDrop = m.top.findNode("backdrop")

    m.zoomCover = m.top.findNode("zoomCover")
    m.zoomCoverBackground = m.top.findNode("zoomCoverBackground")

    m.dscr = m.top.findNode("overview")
    m.dscr.ellipsisText = tr("...")

    m.albumCoverBackground = m.top.findNode("albumCoverBackground")
    m.albumCoverBackground.color = ColorPalette.TRANSPARENT
    m.albumCoverReturnElement = invalid

    m.genres = m.top.findNode("genres")
    drawingStyles = {
        "default": {
            "fontSize": 23,
            "fontUri": "font:MediumSystemFontFile",
            "color": ColorPalette.WHITE
        },
        "b": {
            "fontSize": 23,
            "fontUri": "font:MediumSystemFontFile",
            "color": chainLookupReturn(m.global.session, "user.settings.colorCursor", ColorPalette.HIGHLIGHT)
        }
    }

    if isValid(m.genres)
        m.genres.drawingStyles = drawingStyles
    end if

    m.highlightedGenre = 0

    setFontSizes()

    m.top.optionsAvailable = false

    setupButtons()

    m.albumCover = m.top.findNode("albumCover")
    m.songList = m.top.findNode("songList")
    m.infoGroup = m.top.FindNode("infoGroup")
    m.songListRect = m.top.FindNode("songListRect")

    m.songList.focusFootprintBitmapUri = string.EMPTY
    m.songList.focusBitmapBlendColor = chainLookupReturn(m.global.session, "user.settings.colorCursor", ColorPalette.HIGHLIGHT)
    m.songList.focusFootprintBlendColor = ColorPalette.LIGHTHIGHLIGHT

    m.songListRect.color = ColorPalette.BLACK77

    m.songList.observeField("doneLoading", "onDoneLoading")
end sub

sub setBackdropImage(itemContent)
    ' Use metadata to load backdrop image
    if not isChainValid(itemContent, "json.ArtistItems") then return
    if not isValidAndNotEmpty(itemContent.json.ArtistItems) then return

    artistID = itemContent.json.ArtistItems[0].LookupCI("id")
    if not isValidAndNotEmpty(artistID) then return

    m.LoadBackdropImageTask.itemId = artistID
    m.LoadBackdropImageTask.observeField("content", "onBackdropImageLoaded")
    m.LoadBackdropImageTask.control = TaskControl.RUN
end sub

sub onBackdropImageLoaded()
    m.LoadBackdropImageTask.control = TaskControl.STOP
    data = m.LoadBackdropImageTask.content[0]
    m.LoadBackdropImageTask.unobserveField("content")

    if isValidAndNotEmpty(data)
        if not isStringEqual(m.backDrop.uri, data)
            m.backDrop.uri = data
        end if
    end if
end sub

sub setFontSizes()
    if isValid(m.dscr)
        m.dscr.font.size = 27
    end if

    albumTitle = m.top.findNode("albumTitle")
    if isValid(albumTitle)
        albumTitle.font.size = 45
    end if

    artistName = m.top.findNode("artistName")
    if isValid(artistName)
        artistName.font.size = 23
    end if

    runtime = m.top.findNode("runtime")
    if isValid(runtime)
        runtime.font.size = 23
    end if

    numberofsongs = m.top.findNode("numberofsongs")
    if isValid(numberofsongs)
        numberofsongs.font.size = 23
    end if

    released = m.top.findNode("released")
    if isValid(released)
        released.font.size = 23
    end if

    trackHeading = m.top.findNode("trackHeading")
    if isValid(trackHeading)
        trackHeading.font.size = 23
    end if

    titleHeading = m.top.findNode("titleHeading")
    if isValid(titleHeading)
        titleHeading.font.size = 23
    end if

    lengthHeading = m.top.findNode("lengthHeading")
    if isValid(lengthHeading)
        lengthHeading.font.size = 23
    end if

    playsHeading = m.top.findNode("playsHeading")
    if isValid(playsHeading)
        playsHeading.font.size = 23
    end if
end sub

' Set values for displayed values on screen
sub pageContentChanged()
    item = m.top.pageContent

    setBackdropImage(item)
    setPosterImage(item.posterURL)
    setScreenTitle(item.json)
    setOnScreenTextValues(item.json)
end sub

' Set poster image on screen
sub setPosterImage(posterURL)
    if isValid(posterURL)
        m.albumCover.uri = posterURL
    end if
end sub

' Set screen's title text
sub setScreenTitle(json)
    if not isValidAndNotEmpty(json) then return

    albumTitle = m.top.findNode("albumTitle")
    if isValid(albumTitle)
        albumTitle.text = json.name
    end if

    artistName = m.top.findNode("artistName")
    if isValid(artistName)
        artistName.text = json.AlbumArtist
    end if
end sub

sub adjustForMiniPlayer(audioMiniPlayerVisible = false)
    if not isChainValid(m.songList, "MusicArtistAlbumData") then return
    if m.songList.MusicArtistAlbumData.getChildCount() < 7 then return

    newNumberOfRows = audioMiniPlayerVisible ? 7 : 9

    ' To make Roku redraw with the new row count, we must move the cursor far enough for it to change content
    if m.songList.numRows <> newNumberOfRows
        focusedItem = m.songList.itemFocused
        m.songList.numRows = newNumberOfRows
        m.songList.jumpToItem = m.songList.MusicArtistAlbumData.getChildCount() - 1
        m.songList.jumpToItem = 0
        m.songList.jumpToItem = focusedItem
    end if
end sub

sub onAudioMiniPlayerVisibleChange()
    scene = m.top.getScene()
    audioMiniPlayer = scene.findNode("audioMiniPlayer")
    if isValid(audioMiniPlayer)
        adjustForMiniPlayer(audioMiniPlayer.callFunc("isVisible"))
    end if
end sub

sub onAudioMiniPlayerStateChange()
    if isStringEqual(m.global.audioPlayer.state, MediaPlaybackState.FINISHED)
        refreshAlbumTrackList()
    end if
end sub

sub OnScreenHidden()
    scene = m.top.getScene()
    audioMiniPlayer = scene.findNode("audioMiniPlayer")
    if isValid(audioMiniPlayer)
        audioMiniPlayer.unobserveFieldScoped("visible")
    end if
    m.global.audioPlayer.unobserveFieldScoped("state")
end sub

sub OnScreenShown()
    scene = m.top.getScene()

    audioMiniPlayer = scene.findNode("audioMiniPlayer")
    if isValid(audioMiniPlayer)
        audioMiniPlayer.observeFieldScoped("visible", "onAudioMiniPlayerVisibleChange")
        adjustForMiniPlayer(audioMiniPlayer.callFunc("isVisible"))
    end if
    m.global.audioPlayer.observeFieldScoped("state", "onAudioMiniPlayerStateChange")

    overhang = scene.findNode("overhang")

    if overhang.isVisible
        overhang.isVisible = false
    end if

    if isValid(m.top.lastFocus)
        m.top.lastFocus.setFocus(true)
        group = m.global.sceneManager.callFunc("getActiveScene")
        group.lastFocus = m.top.lastFocus
    else
        m.play.setFocus(true)
        group = m.global.sceneManager.callFunc("getActiveScene")
        group.lastFocus = m.play
    end if

    ' Refresh Track List for Playcount Update
    if m.loadStatus <> ViewLoadStatus.INIT
        refreshAlbumTrackList()
    end if
end sub

sub refreshAlbumTrackList()
    m.focusedItem = m.songList.itemFocused
    m.LoadAlbumTask = CreateObject("roSGNode", "LoadAlbumTask")
    m.LoadAlbumTask.id = m.top.pageContent.id
    m.LoadAlbumTask.observeField("albumDetails", "onAlbumDetailsLoaded")
    m.LoadAlbumTask.control = TaskControl.RUN
end sub

sub onAlbumDetailsLoaded()
    m.LoadAlbumTask.unobserveField("albumDetails")
    m.LoadAlbumTask.control = TaskControl.STOP

    m.songList.doneLoading = false
    m.songList.observeField("doneLoading", "onDoneLoading")

    albumDetails = m.LoadAlbumTask.albumDetails
    m.top.albumData = albumDetails
end sub

' Populate on screen text variables
sub setOnScreenTextValues(json)
    if isValid(json)
        if isValidAndNotEmpty(json.overview)
            ' We have overview text
            setFieldTextValue("overview", json.overview)
        end if

        setFieldTextValue("numberofsongs", `${json.ChildCount} ${tr("Tracks")}`)

        if type(json.ProductionYear) = "roInt"
            setFieldTextValue("released", `${json.ProductionYear}`)
        end if

        if isValidAndNotEmpty(json.genres)
            m.genreList = json.genres

            if m.genreList.count() > 3
                m.genreList = m.genreList.slice(0, 3)
            end if

            m.genres.text = m.genreList.join(", ")
        else
            genresIcon = m.top.findNode("genresIcon")
            genresIcon.GetParent().removeChild(genresIcon)

            m.genres.GetParent().removeChild(m.genres)
            m.genres = invalid
        end if

        if type(json.RunTimeTicks) = "LongInteger"
            setFieldTextValue("runtime", `${ticksToHuman(json.RunTimeTicks)}`)
        end if
    end if
end sub

' Setup playback buttons, default to Play button selected
sub setupButtons()
    m.buttonGrp = m.top.findNode("buttons")
    m.buttonCount = m.buttonGrp.getChildCount()

    m.play = m.top.findNode("play")
    m.play.focusBackground = chainLookupReturn(m.global.session, "user.settings.colorCursor", ColorPalette.HIGHLIGHT)
    m.top.lastFocus = m.buttonGrp

    m.instantMix = m.top.findNode("instantMix")
    m.instantMix.focusBackground = chainLookupReturn(m.global.session, "user.settings.colorCursor", ColorPalette.HIGHLIGHT)

    m.previouslySelectedButtonIndex = -1

    m.top.observeField("selectedButtonIndex", "onButtonSelectedChange")
    m.top.selectedButtonIndex = 0
end sub

' Event handler when user selected a different playback button
sub onButtonSelectedChange()
    ' Change previously focused button back to default
    if m.previouslySelectedButtonIndex > -1
        previousSelectedButton = m.buttonGrp.getChild(m.previouslySelectedButtonIndex)
        previousSelectedButton.focus = false
    end if

    ' Change selected button image to focus state
    selectedButton = m.buttonGrp.getChild(m.top.selectedButtonIndex)
    selectedButton.focus = true
    selectedButton.setfocus(true)
end sub

function focusGenres() as boolean
    if not isValid(m.genres) then return false

    m.highlightedGenre = 0
    highlightGenre()
    m.genres.text = m.genreList.join(", ")

    m.genres.setFocus(true)
    m.top.lastFocus = m.genres

    return true
end function

sub highlightGenre()
    activeGenre = m.genreList[m.highlightedGenre]
    if not isValid(activeGenre) then return

    m.genreList[m.highlightedGenre] = `<b>${activeGenre}</b>`
    m.genres.text = m.genreList.join(", ")
end sub

sub dehighlightGenre()
    activeGenre = m.genreList[m.highlightedGenre]
    if not isValid(activeGenre) then return

    activeGenre = activeGenre.replace("<b>", string.EMPTY).replace("</b>", string.EMPTY)
    m.genreList[m.highlightedGenre] = activeGenre
end sub

sub unfocusGenres()
    if not isValid(m.genres) then return
    dehighlightGenre()
    m.genres.text = m.genreList.join(", ")
end sub

sub unfocusButton()
    m.buttonGrp.getChild(m.top.selectedButtonIndex).escape = string.EMPTY
    selectedButton = m.buttonGrp.getChild(m.top.selectedButtonIndex)
    selectedButton.focus = false
end sub

sub highlightCover(returnElement as object)
    m.albumCoverReturnElement = returnElement
    m.albumCoverBackground.setFocus(true)
    m.albumCoverBackground.color = chainLookupReturn(m.global.session, "user.settings.colorCursor", ColorPalette.HIGHLIGHT)
end sub

sub dehighlightCover()
    returnElementType = m.albumCoverReturnElement.subtype()

    if isStringEqual(returnElementType, "IconButton")
        onButtonSelectedChange()
    else if isStringEqual(returnElementType, "MultiStyleText")
        highlightGenre()
    else if isStringEqual(returnElementType, "Text")
        m.dscr.color = chainLookupReturn(m.global.session, "user.settings.colorCursor", ColorPalette.HIGHLIGHT)
    end if

    m.albumCoverReturnElement.setfocus(true)
    m.top.lastFocus = m.albumCoverReturnElement
    m.albumCoverBackground.color = ColorPalette.TRANSPARENT
end sub

sub zoomAlbumCover()
    if isValidAndNotEmpty(m.zoomCover.uri)
        displayZoomCover()
    end if

    m.zoomCover.uri = m.top.pageContent.posterURL.replace("maxHeight=400", `maxHeight=720`).replace("maxWidth=400", `maxWidth=1920`)
    m.zoomCover.observeFieldScoped("bitmapWidth", "onZoomCoverWidthChange")
    m.zoomCover.observeFieldScoped("bitmapHeight", "onZoomCoverHeightChange")

    displayZoomCover()
end sub

sub displayZoomCover()
    m.zoomCoverBackground.color = ColorPalette.BLACKDD
    m.zoomCoverBackground.visible = true
end sub

sub hideZoomCover()
    m.zoomCoverBackground.visible = false
end sub

sub onZoomCoverWidthChange()
    m.zoomCover.unobserveFieldScoped("bitmapWidth")
    m.zoomCover.width = m.zoomCover.bitmapWidth
    m.zoomCover.translation = [(1920 - m.zoomCover.width) / 2, m.zoomCover.translation[1]]
end sub

sub onZoomCoverHeightChange()
    m.zoomCover.unobserveFieldScoped("bitmapHeight")
    m.zoomCover.height = m.zoomCover.bitmapHeight
    m.zoomCover.translation = [m.zoomCover.translation[0], (1080 - m.zoomCover.height) / 2]
end sub

function onKeyEvent(key as string, press as boolean) as boolean
    if m.buttonGrp.isInFocusChain()
        if key = KeyCode.UP
            if m.buttonGrp.getChild(m.top.selectedButtonIndex).escape = "up"
                ' If there is no overview content, go straight to genre link
                if isStringEqual(m.dscr.text, string.EMPTY)
                    if focusGenres()
                        unfocusButton()
                        return true
                    end if
                else
                    unfocusButton()
                    m.dscr.setFocus(true)
                    m.dscr.color = chainLookupReturn(m.global.session, "user.settings.colorCursor", ColorPalette.HIGHLIGHT)
                    m.top.lastFocus = m.dscr
                    return true
                end if
            end if
        end if

        if key = KeyCode.OK
            if press
                selectedButton = m.buttonGrp.getChild(m.top.selectedButtonIndex)
                selectedButton.selected = not selectedButton.selected
                return true
            end if
        end if

        if key = KeyCode.LEFT
            if m.buttonGrp.getChild(m.top.selectedButtonIndex).escape = "left"
                if m.top.selectedButtonIndex > 0
                    m.previouslySelectedButtonIndex = m.top.selectedButtonIndex
                    m.top.selectedButtonIndex = m.top.selectedButtonIndex - 1
                    return true
                end if
            end if
        end if

        if key = KeyCode.RIGHT
            if m.top.selectedButtonIndex < m.buttonCount - 1
                m.previouslySelectedButtonIndex = m.top.selectedButtonIndex
                m.top.selectedButtonIndex = m.top.selectedButtonIndex + 1
                return true
            else
                selectedButton = m.buttonGrp.getChild(m.top.selectedButtonIndex)
                selectedButton.focus = false
                highlightCover(selectedButton)
            end if
        end if

        if key = KeyCode.DOWN
            if m.buttonGrp.getChild(m.top.selectedButtonIndex).escape = "down"
                m.buttonGrp.getChild(m.top.selectedButtonIndex).escape = ""
                selectedButton = m.buttonGrp.getChild(m.top.selectedButtonIndex)
                selectedButton.focus = false
                m.songList.setFocus(true)
                m.top.lastFocus = m.songList
                return true
            end if
        end if
    end if

    if not press then return false

    if m.zoomCoverBackground.visible
        hideZoomCover()
        return true
    end if

    if m.albumCoverBackground.isInFocusChain()
        if isStringEqual(key, KeyCode.OK)
            zoomAlbumCover()
            return true
        end if

        if isStringEqual(key, KeyCode.LEFT)
            dehighlightCover()
            return true
        end if

        if isStringEqual(key, KeyCode.DOWN)
            m.albumCoverReturnElement = m.songList
            dehighlightCover()
            return true
        end if

        return false
    end if

    if m.songList.hasFocus()
        if key = KeyCode.OPTIONS
            focusedSong = m.songList.content.getChild(m.songList.itemFocused)
            if not isValidAndNotEmpty(focusedSong) then return false

            dialogData = [tr("Add To Playlist")]
            m.global.sceneManager.callFunc("optionDialog", "libraryitem", focusedSong.LookupCI("title") ?? tr("Options"), [], dialogData, { id: focusedSong.LookupCI("id") })

            return true
        end if

        if key = KeyCode.UP
            onButtonSelectedChange()
            return true
        end if
    end if

    if isValid(m.genres)
        if m.genres.isInFocusChain()
            if isStringEqual(key, KeyCode.LEFT)
                if m.highlightedGenre = 0 then return false
                dehighlightGenre()
                m.highlightedGenre--
                highlightGenre()
                return true
            end if

            if isStringEqual(key, KeyCode.RIGHT)
                dehighlightGenre()

                if m.highlightedGenre = m.genreList.count() - 1
                    m.genres.text = m.genreList.join(", ")
                    highlightCover(m.genres)
                    return true
                end if

                m.highlightedGenre++
                highlightGenre()
                return true
            end if

            if isStringEqual(key, KeyCode.OK)
                data = m.top.pageContent.json.GenreItems[m.highlightedGenre]
                data.selectionType = "genre"
                m.top.getScene().jumpTo = data
                return true
            end if

            if isStringEqual(key, KeyCode.DOWN)
                unfocusGenres()

                ' If there is no overview content, go straight to buttons
                if isStringEqual(m.dscr.text, string.EMPTY)
                    selectedButton = m.buttonGrp.getChild(m.top.selectedButtonIndex)
                    selectedButton.focus = true
                    selectedButton.setfocus(true)
                    return true
                else
                    m.dscr.setFocus(true)
                    m.dscr.color = chainLookupReturn(m.global.session, "user.settings.colorCursor", ColorPalette.HIGHLIGHT)
                    m.top.lastFocus = m.dscr
                    return true
                end if
            end if
        end if
    end if

    if m.dscr.hasFocus()
        if key = KeyCode.OK
            createFullDscrDlg()
            return true
        end if

        if isStringEqual(key, KeyCode.RIGHT)
            m.dscr.color = ColorPalette.WHITE
            highlightCover(m.dscr)
            return true
        end if

        if isStringEqual(key, KeyCode.UP)
            if focusGenres()
                m.dscr.color = ColorPalette.WHITE
                return true
            end if
        end if

        if key = KeyCode.DOWN
            m.dscr.color = ColorPalette.WHITE
            selectedButton = m.buttonGrp.getChild(m.top.selectedButtonIndex)
            selectedButton.focus = true
            selectedButton.setfocus(true)
            return true
        end if
    end if

    return false
end function

sub createFullDscrDlg()
    albumTitle = m.top.findNode("albumTitle")
    if isAllValid([albumTitle.text, m.dscr.text])
        m.global.sceneManager.callFunc("standardDialog", albumTitle.text, { data: ["<p>" + m.dscr.text + "</p>"] })
    end if
end sub

sub onDoneLoading()
    m.songList.unobservefield("doneLoading")
    stopLoadingSpinner()

    if m.loadStatus = ViewLoadStatus.INIT
        m.loadStatus = ViewLoadStatus.FIRSTLOAD
    end if

    if isValid(m.focusedItem)
        m.songList.jumpToItem = m.focusedItem
        m.focusedItem = invalid
    end if

    scene = m.top.getScene()
    audioMiniPlayer = scene.findNode("audioMiniPlayer")
    if isValid(audioMiniPlayer)
        adjustForMiniPlayer(audioMiniPlayer.callFunc("isVisible"))
    end if
end sub
