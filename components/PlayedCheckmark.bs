import "pkg:/source/enums/ColorPalette.bs"
import "pkg:/source/utils/misc.bs"

sub init()
    m.top.width = 60
    m.top.height = 50

    m.unplayedNumber = m.top.findNode("unplayedNumber")

    m.checkmark = m.top.findNode("checkmark")
    m.checkmark.font.size = 48

    m.top.color = ColorPalette.TRANSPARENT
    m.checkmark.visible = false
    m.unplayedNumber.visible = false
end sub

sub onDataChange()
    m.top.color = ColorPalette.TRANSPARENT

    if isValid(m.checkmark)
        m.checkmark.color = chainLookupReturn(m.global.session, "user.settings.colorPlayedCheckmarkIcon", ColorPalette.WHITE)
        m.checkmark.visible = false
    end if

    if isValid(m.unplayedNumber)
        m.unplayedNumber.color = chainLookupReturn(m.global.session, "user.settings.colorUnplayedCountTextColor", ColorPalette.WHITE)
        m.unplayedNumber.visible = false
    end if

    if not isValidAndNotEmpty(m.top.data) then return

    if chainLookupReturn(m.top.data, "unplayedCount", 0) > 0
        disableUnwatchedEpisodeCount = chainLookupReturn(m.global.session, "user.settings.`ui.tvshows.disableUnwatchedEpisodeCount`", false)
        if disableUnwatchedEpisodeCount then return

        unplayedCount = m.top.data.unplayedCount.ToStr()
        unplayedCountLength = len(unplayedCount)


        m.top.color = chainLookupReturn(m.global.session, "user.settings.colorPlayedCheckmarkBackground", ColorPalette.TRIADBLUE)

        if isValid(m.unplayedNumber)
            m.unplayedNumber.font.size = unplayedCountLength = 4 ? 25 : 30
            m.unplayedNumber.visible = true
            m.unplayedNumber.text = unplayedCount
        end if
        return
    end if

    if not chainLookupReturn(m.top.data, "showWatchedCheckmark", true) then return

    if chainLookupReturn(m.top.data, "played", false)
        m.top.color = chainLookupReturn(m.global.session, "user.settings.colorPlayedCheckmarkBackground", ColorPalette.TRIADBLUE)
        if isValid(m.checkmark)
            m.checkmark.visible = true
        end if
        return
    end if
end sub
