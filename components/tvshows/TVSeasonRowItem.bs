import "pkg:/source/enums/ColorPalette.bs"
import "pkg:/source/enums/ViewLoadStatus.bs"
import "pkg:/source/utils/config.bs"
import "pkg:/source/utils/misc.bs"

sub init()
    m.title = m.top.findNode("title")
    m.title.font.size = 23

    m.poster = m.top.findNode("poster")
    m.playedIndicator = m.top.findNode("playedIndicator")
    m.loadStatus = ViewLoadStatus.INIT

    m.backdrop = m.top.findNode("backdrop")

    m.backdrop.color = ColorPalette.DARKGREY
    updateSize()
end sub

sub updateSize()
    if m.top.height = 0 or m.top.width = 0 then return

    image = invalid
    if isValid(m.top.itemContent) and isValid(m.top.itemContent.image)
        image = m.top.itemContent.image
    end if

    m.playedIndicator.translation = [m.top.width - m.playedIndicator.width, 0]

    m.backdrop.visible = not isValid(image)

    ' TODO - abstract this in case the parent doesnt have itemSize
    maxSize = [m.top.width, m.top.height]

    ' Always reserve the bottom for the Poster Title
    m.title.maxWidth = maxSize[0]
    m.title.height = 40

    m.poster.width = maxSize[0]
    m.poster.height = maxSize[1] - 40

    m.backdrop.width = m.poster.width
    m.backdrop.height = m.poster.height
end sub

sub onHeightChanged()
    if m.top.height > 0
        updateSize()
    end if
end sub

sub onWidthChanged()
    if m.top.width > 0
        updateSize()
    end if
end sub

sub itemContentChanged() as void
    m.poster = m.top.findNode("poster")
    itemData = m.top.itemContent
    m.title.text = itemData.title

    m.playedIndicator.data = {
        played: chainLookupReturn(itemData, "json.UserData.Played", false),
        unplayedCount: chainLookupReturn(itemData, "json.UserData.UnplayedItemCount", 0)
    }

    imageUrl = itemData.posterURL

    if m.global.session.user.settings["ui.tvshows.blurunwatched"] = true
        if itemData.json.lookup("Type") = "Episode" and isValid(itemData.json.userdata)
            if not itemData.json.userdata.played
                imageUrl = imageUrl + "&blur=15"
            end if
        end if
    end if

    m.poster.uri = imageUrl

    if not isStringEqual(m.loadStatus, ViewLoadStatus.INIT) then return

    updateSize()

    if isStringEqual(m.loadStatus, ViewLoadStatus.INIT)
        m.loadStatus = ViewLoadStatus.FIRSTLOAD
    end if
end sub

'
' Enable title scrolling based on item Focus
sub focusChanged()
    if m.top.itemHasFocus = true
        m.title.repeatCount = -1
        m.title.visible = true
        ' text to speech for accessibility
        if m.global.device.isAudioGuideEnabled = true
            txt2Speech = CreateObject("roTextToSpeech")
            txt2Speech.Flush()
            txt2Speech.Say(m.title.text)
        end if
    else
        m.title.repeatCount = 1
    end if
end sub
