import "pkg:/source/utils/config.bs"
import "pkg:/source/utils/misc.bs"

sub init()
    m.title = m.top.findNode("title")
    m.title.text = tr("Loading...")
    m.overview = m.top.findNode("overview")
    m.poster = m.top.findNode("poster")

    m.rating = m.top.findnode("rating")
    m.infoBar = m.top.findnode("infoBar")
    m.progressBackground = m.top.findNode("progressBackground")
    m.progressBar = m.top.findnode("progressBar")
    m.playedIndicator = m.top.findNode("playedIndicator")

    setFontSizes()
end sub

sub setFontSizes()
    if isValid(m.overview)
        m.overview.font.size = 25
    end if

    if isValid(m.title)
        m.title.font.size = 27
    end if

    communityRating = m.top.findNode("communityRating")
    if isValid(communityRating)
        communityRating.font.size = 23
    end if

    runtime = m.top.findNode("runtime")
    if isValid(runtime)
        runtime.font.size = 23
    end if

    endtime = m.top.findNode("endtime")
    if isValid(endtime)
        endtime.font.size = 23
    end if

    aired = m.top.findNode("aired")
    if isValid(aired)
        aired.font.size = 23
    end if
end sub

sub itemContentChanged()
    itemData = m.top.itemContent

    m.playedIndicator.data = {
        played: chainLookupReturn(itemData, "watched", false),
        unplayedCount: chainLookupReturn(itemData, "json.UserData.UnplayedItemCount", 0)
    }

    if isValid(itemData.json.indexNumber)
        indexNumber = `${itemData.json.indexNumber}. `
        if isValid(itemData.json.indexNumberEnd)
            indexNumber = `${itemData.json.indexNumber}-${itemData.json.indexNumberEnd}. `
        end if
    else
        indexNumber = ""
    end if

    m.title.text = indexNumber + itemData.title
    m.overview.text = itemData.overview

    if isValid(itemData.json.PremiereDate)
        airDate = CreateObject("roDateTime")
        airDate.FromISO8601String(itemData.json.PremiereDate)
        m.top.findNode("aired").text = tr("Aired") + ": " + airDate.AsDateString("short-month-no-weekday")
    end if

    imageUrl = itemData.posterURL

    if m.global.session.user.settings["ui.tvshows.blurunwatched"] = true
        if itemData.lookup("Type") = "Episode"
            if not itemData.userdata.played
                imageUrl = imageUrl + "&blur=15"
            end if
        end if
    end if

    m.poster.uri = imageUrl

    if type(itemData.json.RunTimeTicks) = "roInt" or type(itemData.json.RunTimeTicks) = "LongInteger"
        runTime = getRuntime()
        if runTime < 2
            m.top.findNode("runtime").text = "1 " + tr("min")
        else
            m.top.findNode("runtime").text = `${stri(runTime).trim()} ${tr("mins")}`
        end if

        if m.global.session.user.settings["ui.design.hideclock"] <> true
            m.top.findNode("endtime").text = tr("Ends at %1").Replace("%1", getEndTime())
        end if
    end if

    if chainLookupReturn(m.global.session, "user.settings.`ui.itemdetail.showRatings`", true)
        if isValid(itemData.json.communityRating)
            m.top.findNode("star").visible = true
            m.top.findNode("communityRating").text = str(int(itemData.json.communityRating * 10) / 10)
        else
            m.top.findNode("star").visible = false
        end if
    else
        m.rating.visible = false
        m.infoBar.itemSpacings = [20, -25, 20, 20]
    end if

    ' Add progress bar on bottom (if applicable)
    if chainLookupReturn(itemData, "json.UserData.PlayedPercentage", 0) > 0
        m.progressBackground.width = m.poster.width
        m.progressBackground.visible = true
        progressWidthInPixels = int(m.progressBackground.width * itemData.json.UserData.PlayedPercentage / 100)
        m.progressBar.width = progressWidthInPixels
        m.progressBar.visible = true
    else
        m.progressBackground.visible = false
        m.progressBar.visible = false
    end if

end sub

function getRuntime() as integer
    itemData = m.top.itemContent.json

    ' A tick is .1ms, so 1/10,000,000 for ticks to seconds,
    ' then 1/60 for seconds to minutess... 1/600,000,000
    return int(itemData.RunTimeTicks / 600000000.0)
end function

function getEndTime() as string
    itemData = m.top.itemContent.json
    date = CreateObject("roDateTime")
    duration_s = int(itemData.RunTimeTicks / 10000000.0)
    date.fromSeconds(date.asSeconds() + duration_s)
    date.toLocalTime()

    return formatTime(date)
end function

sub focusChanged()
    if m.top.itemHasFocus = true
        ' text to speech for accessibility
        if m.global.device.isAudioGuideEnabled = true
            txt2Speech = CreateObject("roTextToSpeech")
            txt2Speech.Flush()
            txt2Speech.Say(m.title.text)
            txt2Speech.Say(m.overview.text)
        end if
    end if
end sub
