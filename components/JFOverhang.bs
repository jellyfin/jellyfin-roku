import "pkg:/source/enums/ColorPalette.bs"
import "pkg:/source/enums/KeyCode.bs"
import "pkg:/source/enums/PosterLoadStatus.bs"
import "pkg:/source/utils/config.bs"

sub init()
    m.top.id = "overhang"
    m.top.translation = [54, 0]

    m.leftGroup = m.top.findNode("overlayLeftGroup")
    m.rightGroup = m.top.findNode("overlayRightGroup")
    ' set font sizes
    m.optionText = m.top.findNode("overlayOptionsText")
    m.optionText.font.size = 20
    m.optionStar = m.top.findNode("overlayOptionsStar")
    m.optionStar.font.size = 58
    ' save node references
    m.title = m.top.findNode("overlayTitle")
    m.overlayRightGroup = m.top.findNode("overlayRightGroup")
    m.slideDownAnimation = m.top.findNode("slideDown")
    m.slideUpAnimation = m.top.findNode("slideUp")

    m.profileImage = m.top.findNode("overlayCurrentUserProfileImage")

    m.overlayCurrentUserSelection = m.top.findNode("overlayCurrentUserSelection")
    if isValid(m.overlayCurrentUserSelection)
        m.overlayCurrentUserSelection.blendColor = ColorPalette.HIGHLIGHT
    end if

end sub

sub highlightUser()
    selectUser = m.top.findNode("overlayCurrentUserSelection")
    if isValid(selectUser)
        selectUser.visible = true
    end if
end sub

sub dehighlightUser()
    selectUser = m.top.findNode("overlayCurrentUserSelection")
    if isValid(selectUser)
        selectUser.visible = false
    end if
end sub

sub onVisibleChange()
    if m.top.disableMoveAnimation
        m.top.translation = [54, 0]
        return
    end if
    if m.top.isVisible
        m.slideDownAnimation.control = "start"
        return
    end if

    m.slideUpAnimation.control = "start"
end sub

sub updateTitle()
    m.title.text = m.top.title
end sub

sub updateUser()
    user = m.top.findNode("overlayCurrentUser")
    if isValid(user)
        user.text = m.top.currentUser
    end if
end sub

sub updateUserProfileImage()
    m.profileImage.observeField("loadStatus", "onPosterLoadStatusChanged")
    if isValid(m.profileImage)
        m.profileImage.uri = m.top.currentUserProfileImage
    end if
end sub

sub onPosterLoadStatusChanged()
    if m.profileImage.loadStatus <> PosterLoadStatus.LOADING
        m.profileImage.unobserveField("loadStatus")
    end if

    if m.profileImage.loadStatus = PosterLoadStatus.FAILED
        if m.profileImage.loadWidth = 0
            m.profileImage.uri = "pkg://images/baseline_person_white_48dp.png"
        end if
    end if
end sub

sub updateOptions()
    if m.top.showOptions = true
        m.optionText.visible = true
        m.optionStar.visible = true
    else
        m.optionText.visible = false
        m.optionStar.visible = false
    end if
end sub

function onKeyEvent(key as string, press as boolean) as boolean
    if press
        if key = KeyCode.DOWN
            homeRows = m.top.getscene().findNode("homeRows")
            if isValid(homeRows)
                dehighlightUser()
                homeRows.setfocus(true)
                group = m.global.sceneManager.callFunc("getActiveScene")
                group.lastFocus = homeRows
                return true
            end if
        end if
        if key = KeyCode.OK
            m.top.changeUserSelected = not m.top.changeUserSelected
            dehighlightUser()
            return true
        end if
    end if

    return false
end function
